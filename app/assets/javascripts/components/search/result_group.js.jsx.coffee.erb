SearchResultGroup = React.createClass
  apiBase: "<%= RailsPortal::Application.routes.url_helpers.api_v1_search_search_path %>?"
  getInitialState: ->
    return jQuery.extend({loading: false}, @props)

  onPaginationSelect: (page)->
    if page isnt @state.group.pagination.current_page
      jQuery("##{@pageParam}").val(page)
      query = jQuery('#material_search_form').serialize()

      window.updateSearchUrl(query)

      # strip out all material_types[] params
      query = query.replace(/(^|&)material_types(\[|%5B)(\]|%5D)=[^&]*/, '')

      # add back the one for this group's material type
      query += "&material_types[]=#{@materialType}"

      jQuery.ajax
        dataType: "json"
        url: @apiBase + query
        success: (response)=> @updateState(response)

      @setState({loading: true})

  updateState: (groupData)->
    @replaceState({group: groupData.results[0]})

  render: ->
    group = @state.group
    switch group.type
      when 'investigations'
        @materialType = 'Investigation'
        @pageParam = 'investigation_page'
      else
        @materialType = 'Activity'
        @pageParam = 'activity_page'
    result_items = group.materials.map (material)-> `<SearchResultItem material={material} key={material.id} />`
    bookmark_id = group.type + '_bookmark'
    onSelect = (page)=> @onPaginationSelect(page)
    if @state.loading
      return `(
        <div id={bookmark_id} className={'materials_container ' + group.type}>
          <div className={'material_list_header'}>
            {group.header}
          </div>
          <p className={'border_top'}>
            Finding materials...
          </p>
        </div>
      )`
    return `(
      <div id={bookmark_id} className={'materials_container ' + group.type}>
        <div className={'material_list_header'}>
          {group.header}
        </div>
        <p className={'border_top'}>
          <PaginationInfo info={group.pagination} />
        </p>
        <Pagination info={group.pagination} onSelect={onSelect} />

        <table className={'result_material'} cellPadding='0' cellSpacing='0'>
          <tr>
            <td className={'material_list'}>
              {result_items}
            </td>
          </tr>
        </table>
        <br />
        <Pagination info={group.pagination} onSelect={onSelect} />
      </div>
    )`

window.SearchResultGroup = SearchResultGroup
